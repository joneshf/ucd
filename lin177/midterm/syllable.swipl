castilian_syllable(Syllable) :-
    onset(Onset),
    nucleus(Nucleus),
    coda(Coda),
    append([Onset, Nucleus, Coda], Syllable).

%% `onset`s may be empty.
onset([]).

%% Every `consonant` is an `onset`.
onset([Consonant]) :-
    castilian_phone(Consonant),
    consonant(Consonant).

%% `pl`, `bl`, `kl`, and `gl` are `onset`s.
onset([Consonant, L]) :-
    nonsnt_noncontinuant_noncor_nonvibrant(Consonant),
    alveolar_nonnasal_nonsib(L).

%% `pɾ`, `bɾ`, `tɾ`, `dɾ`, `kɾ`, and `gɾ` are `onset`s.
onset([Consonant, R]) :-
    castilian_phone(Consonant),
    not(snt(Consonant)),
    not(continuant(Consonant)),
    not(palatal(Consonant)),
    not(vibrant(Consonant)),
    nontensed_vibrant(R).

%% `fl` and `fɾ` are `onset`s.
onset([Consonant, LorR]) :-
    castilian_phone(Consonant),
    not(voi(Consonant)),
    continuant(Consonant),
    labial(Consonant),
    (alveolar_nonnasal_nonsib(LorR); nontensed_vibrant(LorR)).

%% Every `vowel` is a nucleus
nucleus([Vowel]) :-
    castilian_phone(Vowel),
    not(consonant(Vowel)).

%% `coda`s may be empty.
coda([]).

%% Every `consonant` is a `coda`.
coda([Consonant]) :-
    castilian_phone(Consonant),
    consonant(Consonant).

%% Given the phones, currently matches `p`, `b`, `k`, `g`.
nonsnt_noncontinuant_noncor_nonvibrant(Consonant) :-
    castilian_phone(Consonant),
    not(snt(Consonant)),
    not(continuant(Consonant)),
    not(cor(Consonant)),
    not(vibrant(Consonant)).

%% Given the phones, currently matches `l`.
alveolar_nonnasal_nonsib(L) :-
    castilian_phone(L),
    alveolar(L),
    not(nasal(L)),
    not(sib(L)).

%% Given the phones, currently matches `ɾ`.
nontensed_vibrant(R) :-
    castilian_phone(R),
    vibrant(R),
    not(tense(R)).
